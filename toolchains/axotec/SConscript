Import('env')

tarball = env.File('toolchain-axo-gcc-4.9.2-arm.tar.gz')

toolchain_dir = env.Dir('%s/toolchain-axo-gcc-4.9.2-arm' %(env['TOOLCHAINS_DIR']))
toolchain_bindir = env.Dir('bin', toolchain_dir)
toolchain_prefix = 'arm-axotec-linux-gnueabi-'

bins = [
  toolchain_bindir.File(toolchain_prefix + 'ar'),
  toolchain_bindir.File(toolchain_prefix + 'as'),
  toolchain_bindir.File(toolchain_prefix + 'gcc'),
  toolchain_bindir.File(toolchain_prefix + 'g++'),
  toolchain_bindir.File(toolchain_prefix + 'gfortran'),
  toolchain_bindir.File(toolchain_prefix + 'ld'),
  toolchain_bindir.File(toolchain_prefix + 'ranlib'),
]

cmd = env.Command([bins], tarball, '$TAR -xf %s -C %s' %(tarball, env['TOOLCHAINS_DIR']))

env.Alias('toolchain-axo-gcc-4.9.2-arm', cmd)

if env.GetOption('target') == 'axotec':
  env.PrependENVPath('PATH', toolchain_bindir.abspath)
  env['CCFLAGS'].extend(['-mcpu=arm926ej-s', '-march=armv5te', '-mtune=arm926ej-s'])
  env['CPPPATH'].append(toolchain_dir.Dir('arm-axotec-linux-gnueabi/sysroot/usr/include'))

  env['AR'] = toolchain_bindir.File(toolchain_prefix + 'ar')
  env['AS'] = toolchain_bindir.File(toolchain_prefix + 'as')
  env['CC'] = toolchain_bindir.File(toolchain_prefix + 'gcc')
  env['CXX'] = toolchain_bindir.File(toolchain_prefix + 'g++')
  env['FC'] = toolchain_bindir.File(toolchain_prefix + 'gfortran')
  env['LD'] = toolchain_bindir.File(toolchain_prefix + 'ld')
  env['RANLIB'] = toolchain_bindir.File(toolchain_prefix + 'ranlib')

Import('env')

tarball = env.File('toolchain-axo-gcc-4.9.2-arm.tar.gz')

toolchain_dir = env.Dir('%s/toolchain-axo-gcc-4.9.2-arm' %(env['TOOLCHAINS_DIR']))
toolchain_bindir = env.Dir('bin', toolchain_dir)
toolchain_prefix = 'arm-axotec-linux-gnueabi-'


cc = env.File(toolchain_prefix + 'gcc', toolchain_bindir)
cxx = env.File(toolchain_prefix + 'g++', toolchain_bindir)
ld = env.File(toolchain_prefix + 'ld', toolchain_bindir)
ar = env.File(toolchain_prefix + 'ar', toolchain_bindir)
strip = env.File(toolchain_prefix + 'strip', toolchain_bindir)

cmd = env.Command([ cc, cxx, ld, ar, strip ], tarball, '$TAR -xf %s -C %s' %(tarball, env['TOOLCHAINS_DIR']))

env.Alias('toolchain-axo-gcc-4.9.2-arm', cmd)

if env.GetOption('target') == 'axotec':
  env.PrependENVPath('PATH', toolchain_bindir.abspath)

  env['CCFLAGS'].extend(['-mcpu=arm926ej-s', '-march=armv5te', '-mtune=arm926ej-s'])

  env['CC'] = cc.abspath
  env['CXX'] = cxx.abspath
  env['LD'] = ld.abspath
  env['AR'] = ar.abspath
  env['STRIP'] = strip.abspath
